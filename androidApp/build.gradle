plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'com.slack.keeper'
}
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.softartdev.noteroom"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 74
        versionName "7.4"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        testInstrumentationRunnerArgument "listener", "leakcanary.FailTestOnLeakRunListener"
        vectorDrawables.useSupportLibrary = true
        resConfigs 'en', 'ru'
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            firebaseCrashlytics.mappingFileUploadEnabled(false)
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    packagingOptions {
        pickFirst "META-INF/AL2.0"
        pickFirst "META-INF/LGPL2.1"
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                '-Xuse-experimental=kotlin.Experimental',
                '-Xopt-in=kotlin.time.ExperimentalTime',
        ]
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    keeper {
        enableL8RuleSharing = true
        enableAssertions.set(false)
    }
    testBuildType "release"
}

dependencies {
    implementation project(':shared')
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.annotation:annotation:1.2.0"
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.vectordrawable:vectordrawable:1.1.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-alpha03"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.activity:activity-ktx:1.2.3'
    implementation 'androidx.fragment:fragment-ktx:1.3.6'
    implementation 'androidx.multidex:multidex:2.0.1'
    def arch_version = "2.1.0"
    implementation "androidx.arch.core:core-common:$arch_version"
    implementation "androidx.arch.core:core-runtime:$arch_version"
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.4.7'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "com.jakewharton.timber:timber:$timber_version"
    def leak_canary_version = '2.7'
    debugImplementation "com.squareup.leakcanary:leakcanary-android-process:$leak_canary_version"
    implementation "com.squareup.leakcanary:plumber-android:$leak_canary_version"
    def koin_version= '3.1.2'
    implementation "io.insert-koin:koin-android:$koin_version"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
    implementation platform('com.google.firebase:firebase-bom:28.3.1')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.4.0'
    testImplementation project(path: ':shared-android-test-util')
    testImplementation "junit:junit:$junit_version"
    testImplementation "io.insert-koin:koin-test:$koin_version"
    testImplementation "io.insert-koin:koin-test-junit4:$koin_version"
    testImplementation "androidx.arch.core:core-testing:$arch_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    testImplementation 'app.cash.turbine:turbine:0.7.0-SNAPSHOT'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:core-ktx:1.4.0'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation "androidx.arch.core:core-testing:$arch_version"
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation "com.squareup.leakcanary:leakcanary-android-instrumentation:$leak_canary_version"
    androidTestUtil 'androidx.test:orchestrator:1.4.0'
}